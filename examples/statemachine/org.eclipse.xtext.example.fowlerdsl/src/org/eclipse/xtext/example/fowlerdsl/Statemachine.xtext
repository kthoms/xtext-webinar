grammar org.eclipse.xtext.example.fowlerdsl.Statemachine with org.eclipse.xtext.common.Terminals

generate statemachine "http://www.eclipse.org/xtext/example/fowlerdsl/Statemachine"

Statemachine :
    {Statemachine}
	('events' 
		events+=Event+ 
	'end')?
	('resetEvents' 
		resetEvents+=[Event]+ 
	'end')?
	('commands' 
		commands+=Command+ 
	'end')?
	states+=State*
;

@Exported
Event:
	name=ID code=ID
;

@Exported
Command:
	name=ID code=ID
;

@Exported
State:
	'state' name=ID
		('actions' '{' actions+=[Command]+ '}')?
		transitions+=Transition*
	'end'
;

@Deprecated
Transition:
	event=[Event] '=>' state=[State]
;
